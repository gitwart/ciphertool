#!/bin/sh
# \
exec tclsh "$0" ${1+"$@"}

# slidebrute --
#
#	Brute force solver for slidefair ciphers.
#
# RCS: @(#) $Id: slidebrute,v 1.5 2008/03/05 03:54:59 wart Exp $
#
# Copyright (C) 2000-2002  Mike Thomas <wart@kobold.org>
# 
# This program is free software; you can redistribute it and/or
# modify it under the terms of the GNU General Public License
# as published by the Free Software Foundation; either version 2
# of the License, or (at your option) any later version.
# 
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
# 
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA  02111-1307, USA.

package require cipher
package require CipherUtil

# Command line processing

if {[llength $argv] < 3 || [llength $argv] > 4} {
    puts stderr "Usage:  $argv0 file ciphertype period ?offset?"
    exit 1
}

set typeList	[list vigenere variant beaufort]
set filename	[lindex $argv 0]
set type	[lindex $argv 1]
set period	[lindex $argv 2]
set offset	[lindex $argv 3]
set alphabet "a b c d e f g h i j k l m n o p q r s t u v w x y z"
set stepInterval  10000

set startIndex 0
set endIndex [expr {$period - 1}]

if {$offset != ""} {
    set startIndex $offset
    set endIndex [expr {$offset + 2}]
}

if {$endIndex > $period} {
    set endIndex [expr {$period - 1}]
}

if {$startIndex > $period} {
    puts stderr "Invalid offset $offset.  Must be less than the period $period"
    exit 1
}

# Read the ciphertext from the input file
set ciphertext [CipherUtil::readCiphertext $filename]

# Convert the ciphertext to pairs of letters

regsub -all { } $ciphertext {} ciphertext
regsub -all {..} $ciphertext {& } ciphertext
set ciphertext [string trimright $ciphertext]

proc decodeSlidefair {ciphertext type key period} {
    set pt {}
    set currentColumn 0
    foreach pair $ciphertext {
	set keyLetter [string index $key $currentColumn]
	set ct1 [string index $pair 0]
	set ct2 [string index $pair 1]

	switch $type {
	    vigenere {
		set pt1 [key convert vigpt $keyLetter $ct2]
		set pt2 [key convert vigct $keyLetter $ct1]
	    }
	    variant {
		set pt1 [key convert varpt $keyLetter $ct2]
		set pt2 [key convert varct $keyLetter $ct1]
	    }
	    beaufort {
		set pt1 [key convert beapt $keyLetter $ct2]
		set pt2 [key convert beact $keyLetter $ct1]
	    }
	    default {
		set pt1 " "
		set pt2 " "
	    }
	}
	if {$pt1 == ""} {
	    set pt1 " "
	}
	if {$pt2 == ""} {
	    set pt2 " "
	}
	if {$pt1 == $ct1} {
	    set pt1 " "
	    set pt2 " "
	}

	append pt $pt1$pt2

	incr currentColumn
	if {$currentColumn >= $period} {
	    set currentColumn 0
	}
    }

    return $pt
}

set maxValue 0
set maxKey 0
set maxPt 0
set count 0
set loopCode {}
for {set i 0} {$i < $period} {incr i} {
    set keyLetter$i { }
}
for {set i $startIndex} {$i <= $endIndex} {incr i} {
    append loopCode "foreach keyLetter$i \"$alphabet\" \{\n"
}
append loopCode {    set key }
for {set i 0} {$i < $period} {incr i} {
    append loopCode "\$keyLetter$i"
}
append loopCode \n
append loopCode "    set pt \[decodeSlidefair \"$ciphertext\" $type \$key $period\]\n"
append loopCode {
    set value [score value $pt]
    if {$value > $maxValue} {
	regsub -all { } $pt {_} pt
	set maxValue $value
	set maxKey $key
	set maxPt $pt
	puts "Key:  $key  Fit:  $value"
	puts "$pt"
    }
    if {$count % $stepInterval == 0} {
	puts "$count:  $key"
    }
    incr count
}
for {set i $startIndex} {$i <= $endIndex} {incr i} {
    append loopCode "\}\n"
}

puts "loopCode = $loopCode"
eval $loopCode

puts "maxValue = $maxValue"
puts "maxKey = $maxKey"
puts "maxPt = $maxPt"
